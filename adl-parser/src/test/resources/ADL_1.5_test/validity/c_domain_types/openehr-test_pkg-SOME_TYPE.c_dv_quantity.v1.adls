archetype (adl_version=1.5)
	openehr-TEST_PKG-SOME_TYPE.c_dv_quantity.v1

language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Thomas Beale">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Illustrates the use of inline dADL representing C_DV_QUANTITY, including with no magnitude">
			keywords = <"ADL", "test">
			copyright = <"copyright (c) 2004 The openEHR Foundation">
		>
	>
	other_details = <
		["regression"] = <"PASS">
	>
	lifecycle_state = <"draft">

definition
	SOME_TYPE[at0000] matches {	-- root item
		standard_quantity_attr matches {
			DV_QUANTITY [at0001] matches {
				-- property matches {"temperature"}
				units matches {"C"}
				magnitude matches {|>=4.0|}
			}
			DV_QUANTITY [at0002] matches {
				-- property matches {"temperature"}
				units matches {"F"}
				magnitude matches {|>=40.0|}
			}
		}
		clinical_quantity_attr_1 matches {
			(C_DV_QUANTITY) <
				assumed_value = <
					units = <"C">
					precision = <0>
					magnitude = <8.0>
				>
				property = <[openehr::127]>
				list = <
					["1"] = <
						units = <"C">
						magnitude = <|>=4.0|>
					>
					["2"] = <
						units = <"F">
						magnitude = <|>=40.0|>
					>
				>
			>
		}
		clinical_quantity_attr_2 matches {
			(C_DV_QUANTITY) <
				property = <[openehr::127]>
				list = <
					["1"] = <
						units = <"C">
					>
					["2"] = <
						units = <"F">
					>
				>
			>
		}
		clinical_quantity_attr_4 matches {
			DV_COUNT matches {
				magnitude matches {|>=0|}
			}
		}
		clinical_quantity_attr_5 matches {
			DV_COUNT matches {
				magnitude matches {|>=0|}
			}
		}
		clinical_quantity_attr_6 matches {
			(C_DV_QUANTITY) <
				property = <[openehr::127]>
				list = <
					["1"] = <
						precision = <|2|>
					>
				>
			>
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					description = <"xxxx">
					text = <"root item">
				>
				["at0001"] = <
					description = <"Centigrade temperature">
					text = <"Centigrade temperature">
				>
				["at0002"] = <
					description = <"Fahrenheit temperature">
					text = <"Fahrenheit temperature">
				>
			>
		>
	>

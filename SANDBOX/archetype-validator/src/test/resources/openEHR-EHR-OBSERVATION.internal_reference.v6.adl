archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.internal_reference.v6

concept
	[at0000]	-- Blood gas assessment
language
	original_language = <[ISO_639-1::en]>
	
description
	original_author = <
		["name"] = <"Sam Heard">
	>
	details = <
		["en"] = <
			copyright = <"copyright (c) 2009 openEHR Foundation">
			language = <[ISO_639-1::en]>
			purpose = <"For recording the arterial or venous blood gases and respiration products.">
			use = <"">
			misuse = <"">
		>
	>
	lifecycle_state = <"AuthorDraft">

definition
	OBSERVATION[at0000] matches {	-- Blood gas assessment
		data matches {
			HISTORY[at0002] matches {	-- history
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0003] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0001] matches {	-- structure
								items cardinality matches {0..*; ordered} matches {
									CLUSTER[at0011] occurrences matches {0..1} matches {	-- Arterial
										items cardinality matches {1..8; ordered} matches {
											ELEMENT[at0006] occurrences matches {0..1} matches {	-- pH
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::119]>
														list = <
															["1"] = <
																units = <"[pH]">
																magnitude = <|0.0..14.0|>
															>
														>
													>
												}
											}
										}
									}
									CLUSTER[at0013] occurrences matches {0..1} matches {	-- Venous
										items cardinality matches {1..4; ordered} matches {
											use_node ELEMENT /data[at0002]/events[at0003]/data[at0001]/items[at0011]/items[at0006]	-- /data[history]/events[Any event]/data[structure]/items[Arterial]/items[pH]
										}
									}
								}
							}
						}
						state matches {
							ITEM_LIST[at0009] matches {	-- state structure
								items cardinality matches {0..*; ordered} matches {
									ELEMENT[at0010] occurrences matches {0..1} matches {	-- FiO2
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::507]>
												list = <
													["1"] = <
														units = <"%">
														magnitude = <|0.0..100.0|>
														precision = <|0|>
													>
												>
												assumed_value = <
													magnitude = <0.0>
													units = <"%">
													precision = <0>
												>
											>
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_LIST[at0020] matches {	-- List
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0021] occurrences matches {0..1} matches {	-- Device
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Blood gas assessment">
					description = <"The assessment of blood gas concentrations and acid-base balance in blood">
				>
				["at0001"] = <
					text = <"structure">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Any event">
					description = <"*">
				>
				["at0006"] = <
					text = <"pH">
					description = <"The negative logarithm of the Hydrogen ion concentration in blood">
				>
				["at0009"] = <
					text = <"state structure">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"FiO2">
					description = <"The fractional concentration of inspired O2 - normally 21%">
				>
				["at0011"] = <
					text = <"Arterial">
					description = <"Arterial readings">
				>
				["at0013"] = <
					text = <"Venous">
					description = <"Venous readings">
				>
				["at0020"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Device">
					description = <"The device used to measure the value">
				>
			>
		>
	>